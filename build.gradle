group 'ru.mts.ngenie'
version '0.1.0'

buildscript {
    ext.kotlin_version = '1.1.4-3'
    ext.docker_version = '1.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "se.transmode.gradle:gradle-docker:$docker_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'docker'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2'
    compile "com.beust:jcommander:1.71"

    testCompile group: 'junit', name: 'junit', version: '4.4'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.mts.ngenie.ApplicationKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task buildDocker(type: Docker, dependsOn: build) {
    applicationName = jar.baseName
    dockerfile = file("${projectDir}/src/main/docker/Dockerfile")
    doFirst {
        copy {
            from jar
            into stageDir
        }
        copy {
            from "${projectDir}/src/main/docker"
            into stageDir
        }
    }
}